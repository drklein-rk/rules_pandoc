"""Declare runtime dependencies

These are needed for local dev, and users must install them as well.
See https://docs.bazel.build/versions/main/skylark/deploying.html#dependencies
"""

load("//pandoc/private:toolchains_repo.bzl", "PLATFORMS", "toolchains_repo")
load("//pandoc/private:versions.bzl", "TOOL_VERSIONS")

########
# Remaining content of the file is only used to support toolchains.
########
_DOC = "Fetch external tools needed for pandoc toolchain"
_ATTRS = {
    "pandoc_version": attr.string(mandatory = True, values = TOOL_VERSIONS.keys()),
    "platform": attr.string(mandatory = True, values = PLATFORMS.keys()),
}

def _pandoc_repo_impl(repository_ctx):
    version = repository_ctx.attr.pandoc_version
    url = "https://github.com/jgm/pandoc/releases/download/{version}/pandoc-{version}-{platform}.zip".format(
        version = version,
        platform = repository_ctx.attr.platform,
    )

    prefix = "pandoc-%s" % version
    if repository_ctx.attr.platform == "arm64-macOS":
        prefix = "%s-arm64" % prefix
    elif repository_ctx.attr.platform == "x86_64-macOS":
        prefix = "%s-x86_64" % prefix

    repository_ctx.download_and_extract(
        url = url,
        integrity = TOOL_VERSIONS[repository_ctx.attr.pandoc_version][repository_ctx.attr.platform],
        strip_prefix = prefix,
    )
    build_content = """# Generated by pandoc/repositories.bzl
load("@rules_pandoc//pandoc:toolchain.bzl", "pandoc_toolchain")

pandoc_toolchain(
    name = "pandoc_toolchain",
    target_tool = select({
        "@bazel_tools//src/conditions:windows": "pandoc.exe",
        "//conditions:default": "bin/pandoc",
    }),
)
"""

    # Base BUILD file for this repository
    repository_ctx.file("BUILD.bazel", build_content)

pandoc_repositories = repository_rule(
    _pandoc_repo_impl,
    doc = _DOC,
    attrs = _ATTRS,
)

# Wrapper macro around everything above, this is the primary API
def pandoc_register_toolchains(name, register = True, **kwargs):
    """Convenience macro for users which does typical setup.

    - create a repository for each built-in platform like "pandoc_linux_amd64"
    - TODO: create a convenience repository for the host platform like "pandoc_host"
    - create a repository exposing toolchains for each platform like "pandoc_platforms"
    - register a toolchain pointing at each platform
    Users can avoid this macro and do these steps themselves, if they want more control.
    Args:
        name: base name for all created repos, like "pandoc1_14"
        register: whether to call through to native.register_toolchains.
            Should be True for WORKSPACE users, but false when used under bzlmod extension
        **kwargs: passed to each pandoc_repositories call
    """
    for platform in PLATFORMS.keys():
        pandoc_repositories(
            name = name + "_" + platform,
            platform = platform,
            **kwargs
        )
        if register:
            native.register_toolchains("@%s_toolchains//:%s_toolchain" % (name, platform))

    toolchains_repo(
        name = name + "_toolchains",
        user_repository_name = name,
    )
